<программа> ::= program \n var <сп.объяв1> begin <сп.опер1> end
<сп.объяв1> ::= <сп.объяв>
<сп.объяв> ::= <объяв>
<сп.объяв> ::= <сп.объяв> <объяв>
<объяв> ::= <сп.ид1> : real \n
<сп.ид1> ::= <сп.ид>
<сп.ид> ::= , id
<сп.ид> ::= <сп.ид> , id
<сп.опер1> ::= <сп.опер> \n
<сп.опер> ::= \n <опер>
<сп.опер> ::= <сп.опер> \n <опер>
<опер> ::= id = <выр1>
<опер> ::= read ( <сп.ид1> )
<опер> ::= write ( <сп.ид1> )
<опер> ::= for id = <выр1> to <выр1> step <выр2> <сп.опер1> next
<опер> ::= if <ЛВ1> <сп.опер1> else <сп.опер1> endif
<выр2> ::= <выр1>
<выр1> ::= <выр>
<выр> ::= <терм1>
<выр> ::= <выр> + <терм1>
<выр> ::= <выр> - <терм1>
<терм1> ::= <терм>
<терм> ::= <множ>
<терм> ::= <терм> * <множ>
<терм> ::= <терм> / <множ>
<множ> ::= ( <выр1> )
<множ> ::= id
<множ> ::= con
<ЛВ1> ::= <ЛВ>
<ЛВ> ::= <ЛТ1>
<ЛВ> ::= <ЛВ> or <ЛТ1>
<ЛТ1> ::= <ЛТ>
<ЛТ> ::= <ЛМ1>
<ЛТ> ::= <ЛТ> and <ЛМ1>
<ЛМ1> ::= <ЛМ>
<ЛМ> ::= <отнош>
<ЛМ> ::= [ <ЛВ1> ]
<ЛМ> ::= not <ЛМ>
<отнош> ::= <выр1> < <выр1>
<отнош> ::= <выр1> <= <выр1>
<отнош> ::= <выр1> > <выр1>
<отнош> ::= <выр1> >= <выр1>
<отнош> ::= <выр1> == <выр1>
<отнош> ::= <выр1> != <выр1>